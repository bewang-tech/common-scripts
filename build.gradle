buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        dependencies {
            classpath "org.ajoberstar:grgit:1.3.0"
        }
    }
}

plugins {
    id 'maven-publish'
    id 'distribution'
}

ext {
    git = org.ajoberstar.grgit.Grgit.open(currentDir: project.projectDir)
    revision = git.head().id
    gitVersion = git.describe().replaceFirst(/^(?i)v/, "")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

group = "com.napster.bi"
version = gitVersion
description = "Rhapsody BI common bash scripts"

distributions {
    main {
        contents {
            into('bin') { from 'bin' }
            into('conf') { from 'conf' }
            into('ddl') { from 'ddl' }
            into('lib') { from 'lib' }
        }
    }
}

distTar {
    classifier = "scripts"
    compression = Compression.GZIP
}

distZip {
    classifier = "scripts"
}

publishing {
    repositories {
        maven {
            name "rhapsody"
            url "http://maven01.internal.rhapsody.com/nexus/content/repositories/releases"
	    credentials {
	      username "$mavenUser"
	      password "$mavenPassword"
	    }
        }
    }
    publications {
        scripts(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            artifact distTar
        }
    }
}
